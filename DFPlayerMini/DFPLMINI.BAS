1 'DFPLAYER MINI CONTROLLER
1000 CLS
1010 TR=1:'TRACK NO
1020 VO=15:'VOLUME
1030 PF$=CHR$(&H7E)+CHR$(&HFF)+CHR$(&H06)
1040 ZR$=CHR$(&H00)
1050 SF$=CHR$(&HEF)
1060 ST=1:'STATUS 0:PAUSE 1:PLAY
1070 RT=0
1500 OPEN "COM0:7,N,8,1,N,N,N,N,N" FOR OUTPUT AS #1
2000 'INITIAL SETUP
2100 'RESET MODULE
2110 PRINT#1,PF$+CHR$(&H0C)+ZR$+ZR$+ZR$+SF$;
2120 FOR I=1 TO 300:NEXT I
2200 'VOLUME SET
2210 PRINT#1,PF$+CHR$(&H06)+ZR$+ZR$+CHR$(VO)+SF$;
2300 'PLAYBACK 1ST TRACK
2310 PRINT#1,PF$+CHR$(&H03)+ZR$+ZR$+CHR$(TR)+SF$;
3000 'TITLE
3010 LOCATE 0,0
3020 PRINT"-=< DFPlayer Mini Controller >=-";
3030 LOCATE 0,1
3040 PRINT"[ PLAY ][PAUSE ][ PREV ][ NEXT ]";
3100 'TRACK AND VOLUME
3110 LOCATE 0,3
3120 PRINT "TRACK:";TR;" VOLUME";VO;
3200 'ANIMATION
3205 IF ST=0 THEN 4000
3210 LOCATE RT+16,2
3220 PRINT " ";
3230 RT=RT+1:IF RT>15 THEN RT=0
3240 LOCATE RT+16,2
3250 PRINT ")";
4000 'SELECT OPERATION
4100 K$=INKEY$
4105 IF K$="" THEN 3100
4110 IF K$=CHR$(244) OR K$="P" THEN LOCATE 0,2:PRINT "PLAY   ";:PRINT#1,PF$+CHR$(&H0D)+ZR$+ZR$+ZR$+SF$;:ST=1
4120 IF K$=CHR$(245) OR K$=" " THEN GOSUB 5000
4130 IF (K$=CHR$(246) OR K$="(") AND TR>1 THEN LOCATE 0,2:PRINT "PREV    ";:PRINT#1,PF$+CHR$(&H02)+ZR$+ZR$+CHR$(TR)+SF$;:TR=TR-1
4140 IF K$=CHR$(247) OR K$=")" THEN LOCATE 0,2:PRINT "NEXT    ";:PRINT#1,PF$+CHR$(&H01)+ZR$+ZR$+CHR$(TR)+SF$;:TR=TR+1
4150 IF K$="+" AND VO<30 THEN LOCATE 0,2:PRINT "VOLUP  ";:PRINT#1,PF$+CHR$(&H04)+ZR$+ZR$+ZR$+SF$;:VO=VO+1
4160 IF K$="-" AND VO>0 THEN LOCATE 0,2:PRINT "VOLDN  ";:PRINT#1,PF$+CHR$(&H05)+ZR$+ZR$+ZR$+SF$;:VO=VO-1
4200 GOTO 3100
5000 'PAUSE OPERATION
5100 IF ST=1 THEN LOCATE 0,2:PRINT "PAUSE   ";:PRINT#1,PF$+CHR$(&H0E)+ZR$+ZR$+ZR$+SF$;:ST=0:RETURN
5110 IF ST=0 THEN LOCATE 0,2:PRINT "RESUME  ";:PRINT#1,PF$+CHR$(&H0D)+ZR$+ZR$+ZR$+SF$;:ST=1:RETURN
5200 RETURN
